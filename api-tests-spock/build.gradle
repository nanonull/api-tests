apply plugin: "groovy"

version = "1.0"
description = "Spock Framework - Example Project"

repositories {
  // Spock releases are available from Maven Central
  mavenCentral()
  // Spock snapshots are available from the Sonatype OSS snapshot repository
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
  // mandatory dependencies for using Spock
  compile "org.codehaus.groovy:groovy-all:2.5.8"
  testCompile platform("org.spockframework:spock-bom:2.0-M1-groovy-2.5")
  testCompile "org.spockframework:spock-core"
  testCompile "org.spockframework:spock-junit4" // you can remove this if your code does not rely on old JUnit 4 rules

  // optional dependencies for using Spock
  testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
  testRuntime "net.bytebuddy:byte-buddy:1.9.3" // allows mocking of classes (in addition to interfaces)
  testRuntime "org.objenesis:objenesis:2.6"    // allows mocking of classes without default constructor (together with CGLIB)

  // dependencies used by examples in this project
  testRuntime "com.h2database:h2:1.4.197"
  testRuntime project(':api-client')
}

// the remaining configuration is specific to this project, and is not required for using Spock

task collectJars(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.testRuntime
}

task runTests(type: Test) {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

